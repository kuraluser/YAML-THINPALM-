buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'org.springframework.boot' version "2.5.4" apply false
    id "name.remal.sonarlint" version "1.5.0" apply false
    id "com.diffplug.gradle.spotless" version "3.28.0" apply false
    id 'com.github.rico.general' version '3.11' apply false
    id "org.sonarqube" version "3.3"
}

// Configurations applied to all subprojects
subprojects {

    group = 'com.cpdss'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    apply plugin: 'com.github.rico.general'
    apply plugin: 'name.remal.sonarlint'
    apply plugin: 'com.diffplug.gradle.spotless'
    apply plugin: 'jacoco'

    ext['log4j2.version'] = '2.17.0'

    // Applying sonarlint plugin
    sonarlint {
        ignoreFailures = true
        excludes {
            source "**${File.separator}generated${File.separator}*"
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled true
        }
    }
    test.finalizedBy jacocoTestReport

    // Applying SonarQube properties
    sonarqube {
        properties {
            property "sonar.sourceEncoding", "UTF-8"
            property "sonar.sources", "src/main/java"
            property "sonar.tests", "src/test"
            property "sonar.exclusions", "**/common/generated/**," +
                                         "**/common/config/**," +
                                         "**/common/exception/**," +
                                         "**/common/grpc/**," +
                                         "**/common/jsonbuilder/**," +
                                         "**/common/logging/**," +
                                         "**/common/redis/**," +
                                         "**/common/rest/**," +
                                         "**/common/scheduler/**," +
                                         "**/common/springdata/**," +
                                         "**/domain/**," +
                                         "**/repository/**," +
                                         "**/AppConfig.java," +
                                         "**/Application.java," +
                                         "**/*Constants.java," +
                                         "**/entity/*.java,"
            property "sonar.language", "java"
            property "sonar.binaries", "build/classes"
            property "sonar.dynamicAnalysis", "reuseReports"
            property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
        }
    }

    // Applying spotless plugin with google java formatter
    spotless {
        java {
            googleJavaFormat()
            licenseHeader '/* Licensed at AlphaOri Technologies */'
        }
    }

    it.tasks.build.dependsOn it.tasks.spotlessApply

    configurations {
        all {
            exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        }
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }
}